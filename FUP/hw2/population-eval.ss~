;;; _______STATE COMPONENTS______

;;; get maze
(define (maze state)
  (car state))

;;; get coordinates list
(define (coordinates state)
  (cadr state))

;;; get x coordinate
(define (coordinate-x state)
  (car (coordinates state)))

;;; get y coordinate
(define (coordinate-y state)
  (cadr (coordinates state)))

;;; get orientation
(define (orientation state)
  (caddr state))

;;; get action sequence
(define (actions state)
  (cond ((null? (cdddr state)) '())
        (else (car (cdddr state)))
))


;;;________ IF-CONDITIONS ________

;;; orientation
(define (north? state)
  (eqv? (orientation state) 'north))

(define (east? state)
  (eqv? (orientation state) 'east))

(define (west? state)
  (eqv? (orientation state) 'west))

(define (south? state)
  (eqv? (orientation state) 'south))

;;; does the field have marks?
(define (mark? state)
  (not (eqv? (cur-field state) 0)))

;;; is there a wall in front of the robot?
(define (wall? state)
  (define x (coordinate-x state))
  (define y (coordinate-y state))
  (cond
    ((north? state) (eqv? (field x (- y 1) state) 'w))
    ((south? state) (eqv? (field x (+ y 1) state) 'w))
    ((east? state)  (eqv? (field (+ x 1) y state) 'w))
    ((west? state)  (eqv? (field (- x 1) y state) 'w))
    )
)

;;; ________ HELPER FUNCTIONS ________

;;; get field type of [x,y] element in maze list
(define (field x y state)
  (list-ref (list-ref (maze state) y) x))

(define (cur-field state)
  (field (coordinate-x state) (coordinate-y state) state))

;;; change value at field
(define (change-field fn x y maze)
  (define (apply-at fn list pos)
    (cond ((= pos 0) (cons (fn (car list)) (cdr list)))
        (#t (cons (car list) (apply-at fn (cdr list) (- pos 1))))
    ))
  (apply-at (lambda (line) (apply-at fn line x)) maze y)
)

; change element of list
(define (change-element lst pos val)
  (cond ((= pos 0) (cons val (cdr lst)))
        (#t (cons (car lst) (change-element (cdr lst) (- pos 1) val)))
    )
  )

;;; find the procedure in program
(define (find-procedure name program)
  (cond
    ((null? program) '()) ; no program name 
    ((and (eqv? (caar program) 'procedure) (eqv? (cadr (car program)) name)) (cddar program)) ; found
    (else (find-procedure name (cdr program))) ; continue by recursive
))

;;; next orientation (for turn-left)
(define (left-orientation state)
  (cond
    ((north? state) 'west)
    ((south? state) 'east)
    ((east? state) 'north)
    ((west? state) 'south)
))

;;; increment the number
(define (inc n)
  (+ n 1))

;;; decrement the number
(define (dec n)
  (- n 1))

;;; return the rest of expr list
(define (expr-upd expr)
  (if (and (not (null? expr)) (not (pair? expr))) '() (cdr expr))
)


;;; __________ ACTIONS __________

;;; Step
(define (step state expr program limit lenlimit)
  (define x (coordinate-x state))
  (define y (coordinate-y state))
  (if (wall? state) (append (list 'exit) state) ;cannot be done -> program emds
      (cond
        ((north? state) (main-func (list (maze state) (list x (- y 1)) (orientation state) (append (actions state) '(step))) (expr-upd expr) program limit lenlimit))
        ((south? state) (main-func (list (maze state) (list x (+ y 1)) (orientation state) (append (actions state) '(step))) (expr-upd expr) program limit lenlimit))
        ((east? state) (main-func (list (maze state) (list (+ x 1) y) (orientation state) (append (actions state) '(step))) (expr-upd expr) program limit lenlimit))
        ((west? state) (main-func (list (maze state) (list (- x 1) y) (orientation state) (append (actions state) '(step))) (expr-upd expr) program limit lenlimit)))))


;;; Turn left
(define (turn-left state expr program limit lenlimit)
  (main-func
   (list (maze state) (coordinates state) (left-orientation state) (append (actions state) '(turn-left)))
   (expr-upd expr) program limit lenlimit)
)

;;; Put mark
(define (put-mark state expr program limit lenlimit)
  (main-func
   (list (change-field inc (coordinate-x state) (coordinate-y state) (maze state)) (coordinates state) (orientation state) (append (actions state) '(put-mark)))
   (expr-upd expr) program limit lenlimit)
)

;;; Get mark
(define (get-mark state expr program limit lenlimit)
  (if
   (eqv? (cur-field state) 0) (append (list 'exit) state) ; no marks -> the action can't be done -> program ends
   (main-func (list (change-field dec (coordinate-x state) (coordinate-y state) (maze state)) (coordinates state) (orientation state) (append (actions state) '(get-mark)))
           (expr-upd expr) program limit lenlimit))
)


;;; ________ MAIN FUNCTION ________

(define (main-func state expr program limit lenlimit)
  (cond
    ; limit is exceeded -> exit
    ((eqv? (car state) 'exit) state)

    ; too much actions
    ((> (length (actions state)) lenlimit) (append (list 'exit) state))
    
    ; empty list -> nop
    ((and (list? expr) (null? expr)) state)
    
    ; commands
    ((eqv? expr 'step) (step state expr program limit lenlimit))
    ((eqv? expr 'turn-left) (turn-left state expr program limit lenlimit))
    ((eqv? expr 'put-mark) (put-mark state expr program limit lenlimit))
    ((eqv? expr 'get-mark) (get-mark state expr program limit lenlimit))

    ; procedures
    ((not (list? expr))
     (if (= limit 0) (append (list 'exit) state) ;limit is exceeded 
       (main-func state (find-procedure expr program) program (- limit 1) lenlimit)) ; call procedure 
     )
    
    ; if-condition: (if <condition> <positive-branch> <negative-branch>)
    ((eqv? (car expr) 'if)
     (let ((condition (cadr expr))
           (positive-branch (caddr expr))
           (negative-branch (cadr (cddr expr))))
       (cond
       ((eqv? condition 'wall?)  (if (wall? state)  (main-func state positive-branch program limit lenlimit) (main-func state negative-branch program limit lenlimit)))
       ((eqv? condition 'north?) (if (north? state) (main-func state positive-branch program limit lenlimit) (main-func state negative-branch program limit lenlimit)))
       ((eqv? condition 'mark?)  (if (mark? state)  (main-func state positive-branch program limit lenlimit) (main-func state negative-branch program limit lenlimit)))
       ))
     )

    ; some sequence
    (else (main-func (main-func state (car expr) program limit lenlimit) (cdr expr) program limit lenlimit))
  )
)

;;; ____________ SIMULATE ____________
(define (simulate state expr program limit lenlimit)
  (define result (main-func state expr program limit lenlimit))
  (if (eqv? (car result) 'exit)
      (list (actions (cdr result)) (list (maze (cdr result)) (coordinates (cdr result)) (orientation (cdr result))))
      (list (actions result) (list (maze result) (coordinates result) (orientation result)))
   )
)


;;; ____________ COMPONENTS ____________

;;; Manhattan distance
(define (manhattan-dist maze1 maze2)
  (define (marks-diff x y) (map (lambda (x y) (map (lambda (x y) (if (eqv? x 'w) 0 (abs (- x y)))) x y)) x y))
  (define (sum elemList) (if (null? elemList) 0 (+ (apply + (car elemList)) (sum (cdr elemList)))))
  (sum (marks-diff maze1 maze2))
)

;;; Configuration distance
(define (config-dist state1 state2)
  (+ (abs (- (coordinate-x state1) (coordinate-x state2))) (abs (- (coordinate-y state1) (coordinate-y state2))) (if (eq? (orientation state1) (orientation state2)) 0 1))
)

;;; The length of the program
(define (len prg)
  (cond
    ((pair? prg) (+ (len (car prg)) (len (cdr prg))))
    ((null? prg) 0)
    ((or (eqv? prg 'procedure) (eqv? prg 'if)) 0)
    (else 1))
)

 
;;; ____________ EVALUATE ____________


; starts a simulation with a specific maze and program
(define (start-simulation prg pair threshold stack-size)
  (let* ((result (simulate (car pair) 'start prg stack-size (list-ref threshold 3)))
         (final-state (cadr result))
         (desired-state (cadr pair)))
    (list
     (manhattan-dist (maze final-state) (maze desired-state))
     (config-dist final-state desired-state)
     0
     (length (car result))
     ))
)

(define (out-of-limit prgvalue threshold)
  (cond
    ((not (pair? prgvalue)) #f)
    ((> (car prgvalue) (car threshold)) #t)
    (else (out-of-limit (cdr prgvalue) (cdr threshold)))
    )
  )

; run program for all maze pairs, returns (<value> <program>)
(define (calculate-prgvalue prg pairs threshold stack-size prgvalue)
  (cond
    ((out-of-limit prgvalue threshold) '())
    ((null? pairs) (list (cons (change-element prgvalue 2 (len prg)) (list prg))))
    (else (calculate-prgvalue prg (cdr pairs) threshold stack-size (map + prgvalue (start-simulation prg (car pairs) threshold stack-size))))
    )
  )

; go through all the programs
(define (my-eval prgs pairs threshold stack-size)
  (cond
    ((null? prgs) '())
    ((> (len (car prgs)) (list-ref threshold 2)) (my-eval (cdr prgs) pairs threshold stack-size))
    (else (append (calculate-prgvalue (car prgs) pairs threshold stack-size '(0 0 0 0)) (my-eval (cdr prgs) pairs threshold stack-size)))
  )
)

;;; evaluate
(define (evaluate prgs pairs threshold stack_size)
  (bubble-sort (my-eval prgs pairs threshold stack_size))
)

;;; ____________ SORT ____________
(define (bubble-sort lst)
  (letrec
    (
     (fix (lambda (f i)
       (if (equal? i (f i))
           i
           (fix f (f i)))))
 
     (sort-step (lambda (lst)
        (if (or (null? lst) (null? (cdr lst)))
            lst
            (if (compare (car lst) (cadr lst))
                (cons (car  lst) (sort-step (cdr lst)))
                (cons (cadr lst) (sort-step (cons (car lst) (cddr lst))))
                ))))
     
     (compare (lambda (x y)
       (if (not (eq? (caar x) (caar y))) (< (caar x) (caar y)) 
           (if (not (eq? (car (cdar x)) (cadar y))) (< (cadar x) (cadar y))
               (if (not (eq? (car (cddar x)) (car (cddr (car y))))) (< (car (cddar x)) (car (cddar y)))
                   (< (car (cdddr (car x))) (car (cdddr (car y)))))))))
     )
  (fix sort-step lst)))
